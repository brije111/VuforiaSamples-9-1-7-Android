apply plugin: 'com.android.application'

def VUFORIA_SDK_DIR = '../vuforia_sdk'

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    sourceSets.main {
        jniLibs.srcDir "$VUFORIA_SDK_DIR/build/lib/"
    }

    defaultConfig {
        applicationId "com.vuforia.engine.CoreSamples"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 910
        versionName "9.1"

        ndk {
            // Default ABI list for this app, can be over-ridden by providing an abiList property
            // e.g. gradle -PabiList=x86 clean assembleDebug
            abiFilters ((project.findProperty('abiList') ?: 'armeabi-v7a, arm64-v8a, x86').split(/,\s*/))
        }
    }

    archivesBaseName = rootProject.projectDir.getName()

    // Ensure that Vuforia Engine datasets placed in the assets folder do not get compressed as
    // this can significantly increase their loading time

    buildTypes {
        release {
            minifyEnabled false
        }
        debug {
            minifyEnabled false
            debuggable true
        }
    }
    androidResources {
        noCompress 'dat'
    }
}

dependencies {
    implementation files("$VUFORIA_SDK_DIR/build/java/Vuforia/Vuforia.jar")
    // ARCore library
    implementation 'com.google.ar:core:1.28.0'
}
